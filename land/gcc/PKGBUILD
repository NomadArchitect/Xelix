pkgname=gcc
pkgver=13.1.0
pkgrel=1
pkgdesc="An optimizing compiler supporting various programming languages, hardware architectures and operating systems."
arch=('i786')
makedepend=('gmp' 'mpfr' 'mpc' 'zlib')
url="https://gcc.gnu.org/"
license=('GPL')
source=(
	"https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
	"$pkgname-$pkgver.patch"
	'xelix.h'
)
sha256sums=(
	'61d684f0aa5e76ac6585ad8898a2427aade8979ed5e7f85492286c4dfc13ee86'
	'SKIP'
	'SKIP'
)

prepare() {
	patch -p0 < $pkgname-$pkgver.patch
	cp xelix.h $pkgname-$pkgver/gcc/config/
}

build() {
	rm -r build || true
	mkdir build
	cd build

	unset CFLAGS CPPFLAGS CXXFLAGS
	export CFLAGS="-D__int64_t_defined"
	export CPPFLAGS="-D__int64_t_defined"
	../gcc-$pkgver/configure \
		--host=i786-pc-xelix \
		--target=i786-pc-xelix \
		--with-gmp=$SYSROOT/usr \
		--with-mpfr=$SYSROOT/usr \
		--with-mpc=$SYSROOT/usr \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--enable-languages=c,c++ \
		--with-system-zlib \
		--enable-shared

	make -j$(nproc) all-gcc
}

package() {
	cd build
	make DESTDIR=$pkgdir install-gcc
}

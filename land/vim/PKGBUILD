pkgname=vim
pkgver=8.1
pkgrel=1
pkgdesc="Vim is a highly configurable text editor built to make creating and changing any kind of text very efficient."
arch=('i786')
makedepend=('ncurses')
url="https://www.vim.org/"
source=('ftp://ftp.vim.org/pub/vim/unix/vim-8.1.tar.bz2')
sha256sums=('8b69fbd01c877dd8ecbbeca1dc66e5e927228d631ac4c2174b9307eb5c827c86')

build() {
	cd vim81

	ac_cv_sizeof_int=4 \
	vim_cv_getcwd_broken=no \
	vim_cv_memmove_handles_overlap=yes \
	vim_cv_stat_ignores_slash=yes \
	vim_cv_tgetent=zero \
	vim_cv_terminfo=yes \
	vim_cv_toupper_broken=no \
	vim_cv_tty_group=world \
	./configure \
	--host=i786-pc-xelix \
	--prefix=/usr \
	--sysconfdir=/etc \
	--with-tlib=ncurses

	cd src && sh osdef.sh && cd ..
	make
}

package() {
	cd vim81
	make DESTDIR=$pkgdir install
	ln -s vim $pkgdir/usr/bin/vi
}
